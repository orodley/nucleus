(defvar $builtin-functions
  (list
    (cons 'float builtin-float)
    (cons 'int builtin-int)
    (cons '+ builtin+)
    (cons '- builtin-)
    (cons '* builtin*)
    (cons '% builtin%)
    (cons '/ builtin/)
    (cons 'cons builtin-cons)
    (cons 'car builtin-car)
    (cons 'cdr builtin-cdr)
    (cons 'eq? builtin-eq?)
    ;; TODO: these should be variable arity. maybe a macro which expands into
    ;; the binary version?
    (cons '< builtin-<)
    (cons '<= builtin-<=)
    (cons '> builtin->)
    (cons '>= builtin->=)))
(defvar $special-forms
  (list
    (cons 'set special-form-set)
    (cons 'let special-form-let)
    (cons 'progn special-form-progn)
    (cons 'if special-form-if)
    (cons 'quote special-form-quote)
    (cons 'lambda special-form-lambda)))
(defvar $builtin-macros ;; TODO: should be in the stdlib
  (list
    (cons 'let* builtin-macro-let*)
    (cons 'cond builtin-macro-cond)
    (cons 'case builtin-macro-case)
    (cons 'and builtin-macro-and)
    (cons 'or builtin-macro-or)
    (cons 'when builtin-macro-when)
    (cons 'unless builtin-macro-unless)
    ;; TODO: these should just be regular functions once we have varargs
    (cons 'format builtin-macro-format)
    (cons 'list builtin-macro-list)))
;; TODO: Write these as more readable constant expressions
;; TODO: Maybe these should have different names to indicate that this
;; representation is only used when they are dynamic values?

;; nil is just a null cons pointer, so the dynamic version is just the cons
;; lowtag.
(defvar LLVM-NIL (llvm-i64<-int CONS-LOWTAG))
(defvar LLVM-NULL-CONS (LLVMConstNull CONS-CELL-PTR))
(defvar NIL-TERM (make-term 'cons LLVM-NULL-CONS false))

(defvar LLVM-TRUE (llvm-i64<-int 263))
(defvar LLVM-FALSE (llvm-i64<-int 519))
(defvar LLVM-FIXNUM-T (llvm-i64<-int 775))
(defvar LLVM-CONS-T (llvm-i64<-int 1031))
(defvar LLVM-BOOL-T (llvm-i64<-int 1543))
(defvar LLVM-FLOAT-T (llvm-i64<-int 1799))
(defvar LLVM-SYMBOL-T (llvm-i64<-int 2055))
(defvar LLVM-FOREIGN-T (llvm-i64<-int 2311))
(defvar LLVM-STRING-T (llvm-i64<-int 2567))
(defvar LLVM-LAMBDA-T (llvm-i64<-int 2823))
;; TODO: these should all have static types
(defvar $constants
  (list
    (make-constant 'nil LLVM-NULL-CONS 'cons)
    (make-constant 'true (llvm-i32<-int 1) 'bool)
    (make-constant 'false (llvm-i32<-int 0) 'bool)
    (make-constant 'fixnum-t LLVM-FIXNUM-T '?)
    (make-constant 'cons-t LLVM-CONS-T '?)
    (make-constant 'bool-t LLVM-BOOL-T '?)
    (make-constant 'float-t LLVM-FLOAT-T '?)
    (make-constant 'symbol-t LLVM-SYMBOL-T '?)
    (make-constant 'foreign-t LLVM-FOREIGN-T '?)
    (make-constant 'string-t LLVM-STRING-T '?)
    (make-constant 'lambda-t LLVM-LAMBDA-T '?)))

(defun get-special-form (name) (assoc-cdr name $special-forms))
(defun get-builtin-function (name) (assoc-cdr name $builtin-functions))
(defun get-builtin-macro (name) (assoc-cdr name $builtin-macros))

(defun special-form-set (context args)
  (let* ((env (context-env context))
         (name (first args))
         (binding (get-binding context name))
         (new-value (compile-expr context (second args))))
    (cond
      ((nil? binding)
       (fail! context (format nil "Undefined variable '~'" name)))
      ((not (binding-lvalue? binding))
       (fail! context (format nil "Cannot assign to constant '~'" name)))
      (true (LLVMBuildStore $builder (tv (coerce context new-value
                                                 (tt (binding-term binding))))
                            (tv (binding-term binding)))))
    new-value))

(defun special-form-let (context args)
  ;; TODO: better error reporting for malformed 'let' clauses
  ;; TODO: optional type annotations for 'let' variables
  (let* ((clauses (first args))
         (body (rest args))
         (names (map first clauses))
         (terms (map (lambda (clause) (compile-expr context (second clause)))
                      clauses))
         (new-context (extend-env-with-terms context names terms)))
    ;(format true "After extending environment with variables ~, the new environment is ~" names (context-env new-context))
    (compile-progn new-context body)))

(defun builtin-macro-let* (context args)
  ;; TODO: Life without quasiquote is painful
  (if (nil? (first args))
    (cons 'progn (rest args))
    (list 'let (list (caar args))
          (cons 'let* (cons (cdar args) (rest args))))))

(defun special-form-progn (context args)
  (compile-progn context args))

(defun special-form-if (context args)
  (let* ((condition (coerce context (compile-expr context (first args)) 'bool))
         (then-expr (second args))
         ;; TODO: maybe during the 'macroexpansion' pass we insert missing
         ;; else branches? We could perhaps do some other things too, like what
         ;; GHC does with desugaring into 'Core'.
         (else-expr (if (length-is? args 2) 'nil (third args)))
         (current-func (get-current-func $builder))
         (then-block (LLVMAppendBasicBlock current-func "then"))
         (else-block (LLVMAppendBasicBlock current-func "else"))
         (after-block (LLVMAppendBasicBlock current-func "after")))
    (LLVMBuildCondBr $builder (LLVMBuildIntCast $builder (tv condition) (LLVMIntType 1) "")
                     then-block else-block)
    (let* ((then-term (compile-in-block context then-expr then-block))
           (then-resultant-block (LLVMGetInsertBlock $builder))
           (else-term (compile-in-block context else-expr else-block))
           (else-resultant-block (LLVMGetInsertBlock $builder))
           (then-type (tt then-term))
           (else-type (tt else-term))
           (result-type
             (if (eq? then-type else-type)
               then-type
               ;; TODO: If both types are different static types then we need
               ;; a dynamic value, but we shouldn't just throw away the static
               ;; type information - we should keep it as a union type and do
               ;; static type checking based on that. For example:
               ;;  (car (if foo "" false))
               ;; should be able to be detected as a static type error, as the
               ;; type of the argument of car is 'cons' (or (cons A B) once we
               ;; have a proper cons type), which is disjoint from the type of
               ;; the if expression, (or string bool).
               '?))
           ;; We need to call 'coerce' while the builder is in the same block
           ;; as the value we are calling it on, otherwise the type coercion
           ;; code is inserted into the wrong place. However, we only know what
           ;; type to coerce into after we have already compiled both branches,
           ;; so we have to go back into the block.
           (then-term (%build-branch context then-term result-type
                                     then-resultant-block after-block))
           (else-term (%build-branch context else-term result-type
                                     else-resultant-block after-block)))
      (LLVMPositionBuilderAtEnd $builder after-block)
      ;(format true "(if ~ ~) -> ~" then-type else-type result-type)
      (make-term
        result-type
        ;; TODO: maybe llvm-phi should just figure out the result type itself
        ;; based on the the given terms and do the coercions itself.
        (llvm-phi $builder (llvm-type<-type-spec context result-type)
                  (list (cons (tv then-term) then-resultant-block)
                        (cons (tv else-term) else-resultant-block)))
        false))))

(defun compile-in-block (context expr block)
  (LLVMPositionBuilderAtEnd $builder block)
  (compile-expr context expr))

(defun %build-branch (context term typ branch-block after-block)
  (LLVMPositionBuilderAtEnd $builder branch-block)
  (let ((result (coerce context term typ)))
    (LLVMBuildBr $builder after-block)
    result))

(defun builtin-macro-cond (context clauses)
  (if (nil? clauses)
    'nil
    ;; TODO: Life without quasiquote is painful
    (list 'if (caar clauses)
          (cons 'progn (cdar clauses))
          (cons 'cond (cdr clauses)))))

(defun builtin-macro-case (context args)
  (let ((expr (first args))
        (clauses (rest args))
        (case-sym (gensym)))
    ;; TODO: Life without quasiquote is painful
    (list 'let (list (list case-sym expr))
          (cons 'cond
                (map (lambda (clause)
                       (cons (if (eq? (first clause) 'default)
                               'true
                               (list 'eq? case-sym (first clause)))
                             (rest clause)))
                     clauses)))))

(defun special-form-quote (context args)
  (let ((to-quote (first args)))
    (case (type to-quote)
      (fixnum-t (compile-expr context to-quote))
      (cons-t (if (nil? to-quote)
                ;; This case never comes up directly from source, as 'nil' is
                ;; read in as the symbol 'nil' not the constant. It only comes
                ;; up in the recursive case below when quoting a cons.
                NIL-TERM
                (compile-expr context (list 'cons
                                            (list 'quote (car to-quote))
                                            (list 'quote (cdr to-quote))))))
      (symbol-t (make-term
                  'symbol
                  (llvm-build-call
                    $builder
                    (runtime-func "rt_intern_symbol"
                                  (llvm-type<-type-spec context'symbol)
                                  (list CHAR*))
                    (list (global-string (string<-symbol to-quote)))
                    "intern-const")
                  false))
      (default (fail! context (format nil "Don't know how to quote '~'" to-quote))))))

(defun builtin-macro-and (context operands)
  (if (nil? operands)
    'true
    (let ((operand-sym (gensym)))
      ;; TODO: Life without quasiquote is painful
      (list 'let
            (list (list operand-sym (first operands)))
            (list 'if operand-sym
                  (cons 'and (rest operands))
                  'false)))))

(defun builtin-macro-or (context operands)
  (if (nil? operands)
    'false
    (let ((operand-sym (gensym)))
      ;; TODO: Life without quasiquote is painful
      (list 'let
            (list (list operand-sym (first operands)))
            (list 'if operand-sym
                  'true
                  (cons 'or (rest operands)))))))

;;; TODO: Detect lambdas with identical bodies and use the same function.
;;; We'd also need to keep track if any errors were found when compiling the
;;; other lambda and generate the same error (once we're doing errors properly).

(defvar $lambda-counter 0)
(defun special-form-lambda (context args-and-body)
  (let* ((args (car args-and-body))
         (body (cdr args-and-body))
         (name (symbol<-string
                 (format nil "lambda_~" (set $lambda-counter
                                             (+ 1 $lambda-counter)))))
         (current-block (LLVMGetInsertBlock $builder))
         (captures (find-captured-vars context body args))
         (func (compile-lambda context name args body captures)))
    ;; compile-lambda will change the builders position.
    (LLVMPositionBuilderAtEnd $builder current-block)
    (make-lambda context func (length args) captures)))

(defun builtin-macro-format (context args)
  ;; TODO: Life without quasiquote is painful
  (list '%format (first args) (second args)
        (cons 'list (cddr args))))

(defun builtin-macro-list (context args)
  (if (nil? args)
    'nil
    ;; TODO: Life without quasiquote is painful
    (list 'cons (first args) (cons 'list (rest args)))))

(defun builtin-macro-unless (context args)
  (let ((condition (first args))
        (body (rest args)))
    ;; TODO: Life without quasiquote is painful
    (list 'if condition 'nil (cons 'progn body))))

(defun builtin-macro-when (context args)
  (let ((condition (first args))
        (body (rest args)))
    ;; TODO: Life without quasiquote is painful
    (list 'if condition (cons 'progn body))))


;;; TODO: We don't need to bother removing the lowtag for some of these numeric
;;; functions when both are dynamic. Maybe we should come up with a nice way of
;;; enabling this kind of optimisation.

(defun builtin-float (context args)
  (make-term
    'float
    (implicit-conversion context (coerce context (first args) 'int) 'float)
    false))
(defun builtin-int (context args)
  (make-term
    'int
    (implicit-conversion context (coerce context (first args) 'float) 'int)
    false))

(defun comparison (context cmp int-cmp float-cmp operands)
  (if (not (length-is? operands 2))
    (fail! context (format nil "~ can only take two operands" cmp)))
  (let* ((type-and-vals (arithmetic-conversion context operands))
         (typ (car type-and-vals))
         (vals (cdr type-and-vals)))
    (make-term
      'bool
      (LLVMBuildZExt
        $builder
        (if (floating-point? typ)
          (LLVMBuildFCmp $builder float-cmp (first vals) (second vals) cmp)
          (LLVMBuildICmp $builder int-cmp (first vals) (second vals) cmp))
        (LLVMIntType 32)
        "")
      false)))
(defun builtin-< (context args) (comparison context "<" LLVMIntSLT LLVMRealOLT args))
(defun builtin-<= (context args) (comparison context "<=" LLVMIntSLE LLVMRealOLE args))
(defun builtin-> (context args) (comparison context ">" LLVMIntSGT LLVMRealOGT args))
(defun builtin->= (context args) (comparison context ">=" LLVMIntSGE LLVMRealOGE args))

(defun arithmetic-builtin (context args int-op float-op name)
  (let* ((type-and-vals (arithmetic-conversion context args))
         (typ (car type-and-vals))
         (vals (cdr type-and-vals))
         (op (if (floating-point? typ) float-op int-op)))
    (make-term
      typ (reduce (lambda (a b) (op $builder a b name false)) vals) false)))
(defun builtin+ (context args)
  (arithmetic-builtin context args %LLVMBuildAdd %LLVMBuildFAdd "+"))
(defun builtin- (context args)
  (arithmetic-builtin context args %LLVMBuildSub %LLVMBuildFSub "-"))
(defun builtin* (context args)
  (arithmetic-builtin context args %LLVMBuildMul %LLVMBuildFMul "*"))
(defun builtin% (context args)
  (arithmetic-builtin context args %LLVMBuildSRem %LLVMBuildFRem "%"))
(defun builtin/ (context args)
  (arithmetic-builtin context args %LLVMBuildSDiv %LLVMBuildFDiv "/"))

;;; TODO: Remove these. They only exist as a workaround because we don't store
;;; RTTI on lambdas yet. If we pass these functions as values the caller
;;; assumes that all the arguments are i64s. So we make wrappers that take i64s.
(defun %LLVMBuildAdd (builder val1 val2 name) (LLVMBuildAdd builder val1 val2 name))
(defun %LLVMBuildSub (builder val1 val2 name) (LLVMBuildSub builder val1 val2 name))
(defun %LLVMBuildMul (builder val1 val2 name) (LLVMBuildMul builder val1 val2 name))
(defun %LLVMBuildSRem (builder val1 val2 name) (LLVMBuildSRem builder val1 val2 name))
(defun %LLVMBuildSDiv (builder val1 val2 name) (LLVMBuildSDiv builder val1 val2 name))
(defun %LLVMBuildFAdd (builder val1 val2 name) (LLVMBuildFAdd builder val1 val2 name))
(defun %LLVMBuildFSub (builder val1 val2 name) (LLVMBuildFSub builder val1 val2 name))
(defun %LLVMBuildFMul (builder val1 val2 name) (LLVMBuildFMul builder val1 val2 name))
(defun %LLVMBuildFRem (builder val1 val2 name) (LLVMBuildFRem builder val1 val2 name))
(defun %LLVMBuildFDiv (builder val1 val2 name) (LLVMBuildFDiv builder val1 val2 name))

(defun builtin-cons (context args)
  (make-term
    'cons ; TODO: this should be a higher-order type determined by the type of the input terms
    (llvm-build-call
      $builder
      (runtime-func "rt_cons" CONS-CELL-PTR (list NUC-VAL NUC-VAL))
      (map (lambda (term) (tv (coerce context term '?))) args)
      "cons")
    false))

;;; TODO: An interesting idea is to make car and cdr return lvalues so that we
;;; can do stuff like (set (car foo) bar). Have to give it a try and see if
;;; that's a good design for generalized references. We'd also need a special
;;; type for references, and make functions choose to return references. e.g.:
;;; (defun first ((c cons)) -> (ref ?) (car c))
;;; otherwise the 'lvalueness' gets swallowed upon returning from the function.
;;; TODO: These should coerce their argument to a cons once we have proper
;;; static typing for conses.

(defun builtin-car (context args)
  (let ((cons-ptr (tv (coerce context (first args) 'cons))))
    (make-term
      '? ; TODO: this should be determined by the type of the input term
      (LLVMBuildLoad $builder (LLVMBuildStructGEP $builder cons-ptr 0 "") "car")
      false)))

(defun builtin-cdr (context args)
  (let ((cons-ptr (tv (coerce context (first args) 'cons))))
    (make-term
      '? ; TODO: this should be determined by the type of the input term
      (LLVMBuildLoad $builder (LLVMBuildStructGEP $builder cons-ptr 1 "") "cdr")
      false)))

(defun builtin-eq? (context args)
  (let ((term1 (first args))
        (term2 (second args)))
    (cond
      ((and (eq? (tt term1) '?) (eq? (tt term2) '?))
       ;; TODO: If we have two dynamic values that are both foreign pointers
       ;; they can be different as they are boxed. In that case we need to
       ;; unbox them.
       (simple-int-cmp (tv term1) (tv term2)))
      ((eq? (tt term1) '?)
       ;; We compare the runtime type, and if they're the same we compare the
       ;; actual value.
       ;; TODO: This is a little inefficient as coerce checks the type after
       ;; we've already checked it to even reach that block. A version of
       ;; 'coerce' that assumes the correct type could be handy.
       (let* ((runtime-type (llvm-build-call
                              $builder
                              (runtime-func "rt_type" NUC-VAL (list NUC-VAL))
                              (list (tv term1))
                              ""))
              (correct-type?
                (LLVMBuildICmp $builder LLVMIntEQ runtime-type
                               (type-id<-type-spec context (tt term2))
                               ""))
              (current-func (get-current-func $builder))
              (correct-type-block (LLVMAppendBasicBlock current-func "type-eq"))
              (wrong-type-block (LLVMAppendBasicBlock current-func "type-not-eq"))
              (after-block (LLVMAppendBasicBlock current-func "eq?-after")))
         (LLVMBuildCondBr $builder correct-type?  correct-type-block wrong-type-block)
         (LLVMPositionBuilderAtEnd $builder wrong-type-block)
         (LLVMBuildBr $builder after-block)
         (LLVMPositionBuilderAtEnd $builder correct-type-block)
         (let ((ct-term (builtin-eq?
                          context (list (coerce context term1 (tt term2)) term2))))
           (LLVMBuildBr $builder after-block)
           (LLVMPositionBuilderAtEnd $builder after-block)
           (make-term
             'bool
             (llvm-phi $builder (llvm-type<-type-spec context 'bool)
                       (list (cons (tv ct-term) correct-type-block)
                             (cons (llvm-i32<-int 0) wrong-type-block)))
             false))))
      ((eq? (tt term2) '?)
       ;; Reverse the order so we go into the case above where the type of the
       ;; first term is ?
       (builtin-eq? context (list term2 term1)))
      ((eq? (tt term1) (tt term2))
       (simple-int-cmp (tv term1) (tv term2)))
      ((and (numeric? (tt term1)) (numeric? (tt term2)))
       (let* ((type-and-vals (arithmetic-conversion context (list term1 term2)))
              (typ (car type-and-vals))
              (vals (cdr type-and-vals)))
         (if (floating-point? typ)
           (make-term
             'bool
             (LLVMBuildFCmp $builder LLVMRealOEQ (tv term1) (tv term2) "")
             false)
           (simple-int-cmp (first vals) (second vals)))))
      (true
        (make-term 'bool (llvm-i32<-int 0) false)))))
