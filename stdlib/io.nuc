;;;; This file defines all functions relating to input and output.

(extern (rt_write_char_to_stream write-char-to-stream) (? ?) -> ?)
(extern (rt_read_char_from_stream read-char-from-stream) (?) -> ?)
(extern (rt_write_string_to_stream write-string-to-stream) (? ?) -> ?)
(extern (rt_write_string_ln_to_stream write-string-ln-to-stream) (? ?) -> ?)
(extern (rt_write_addr_to_stream %write-addr-to-stream) (? ?) -> ?)
(extern (rt_symbol_to_string string<-symbol) (?) -> ?)
(extern (rt_string_to_symbol symbol<-string) (?) -> ?)
(extern (rt_make_string_stream make-string-stream) (?) -> ?)
(extern (rt_string_stream_to_string string<-string-stream) (?) -> ?)

(defun read-char ()
  (read-char-from-stream $stdin))

(defun write-char (c)
  (write-char-to-stream $stdout c))

(defun write-string (str)
  (write-string-to-stream $stdout str))

(defun write-string-ln (str)
  (write-string-ln-to-stream $stdout str))

(defun print (sexp)
  (print-to-stream $stdout sexp))

(defun println (sexp)
  (print sexp)
  (write-string-ln ""))

(defun plr (sexp)
  (println sexp)
  sexp)

(defun print-to-stream (stream sexp)
  (case (type sexp)
    (fixnum-t (print-fixnum stream sexp))
    (cons-t (print-cons stream sexp))
    (symbol-t (write-string-to-stream stream (string<-symbol sexp)))
    (string-t
      (write-char-to-stream stream 34)
      (write-string-to-stream stream sexp)
      (write-char-to-stream stream 34))
    (lambda-t (write-string-to-stream stream "#<lambda>"))
    (bool-t (write-string-to-stream stream (if sexp "true" "false")))
    (foreign-t (%write-addr-to-stream stream sexp))
    (default
      (panic! "Can't print this type"))))

(defun print-fixnum (stream x)
  (if (< x 10)
    (write-char-to-stream stream (+ x 48))
    (let* ((ndigits (ceiling (log10 (float<-int x))))
           (multiplier (expt 10 (- ndigits 1))))
      (write-char-to-stream stream (+ (/ x multiplier) 48))
      (print-fixnum stream (- x (* (/ x multiplier) multiplier))))))

(defun print-cons (stream c)
  (cond
    ((nil? c) (write-string-to-stream stream "nil"))
    (true (write-string-to-stream stream "(")
          (print-to-stream stream (car c))
          (%print-tail stream (cdr c)))))

(defun %print-tail (stream c)
  (cond
    ((nil? c)
     (write-string-to-stream stream ")"))
    ((cons? c)
     (write-string-to-stream stream " ")
     (print-to-stream stream (car c))
     (%print-tail stream (cdr c)))
    (true
     (write-string-to-stream stream " . ")
     (print-to-stream stream c)
     (write-string-to-stream stream ")"))))


(defvar $unread-char nil)

(defun %read-char (stream)
  (cond
    ((nil? $unread-char)
     (read-char-from-stream stream))
    (true
      (let ((c $unread-char))
        (set $unread-char nil)
        c))))

(defun back-up-char (c)
  (if (nil? $unread-char)
    (set $unread-char c)
    ;; We should only ever need to do this once before reading another char
    (panic! "Tried to back up when there was already an unread char")))

;;; When and only when EOF is reached, read returns a value eq? to this
(defvar EOF (cons nil nil))

(defun read ()
  (read-from-stream $stdin))

(defun read-all (stream)
  (let ((list-head (cons nil nil)))
    (%read-all list-head stream)
    (cdr list-head)))

(defun %read-all (list-head stream)
  (let ((sexp (read-from-stream stream)))
    (cond
      ((not (eq? sexp EOF))
       (set-cdr list-head (cons sexp nil))
       (%read-all (cdr list-head) stream)))))

(defun read-from-stream (stream)
  (let ((c (%read-char stream)))
    (cond
      ((eq? c 59)
       (skip-comment stream)
       (read-from-stream stream))
      ((whitespace? c)
       (read-from-stream stream))
      ((eq? c 34)
       (let ((list-head (cons nil nil)))
         (read-string stream list-head)
         (string<-char-list (cdr list-head))))
      ((and (<= 48 c) (<= c 57)) (read-fixnum stream (- c 48)))
      ((eq? c 40)
       (let ((list-head (cons nil nil)))
         (read-list stream list-head)
         (cdr list-head)))
      ((eq? c 39)
       (let ((quoted (read-from-stream stream)))
         (when (eq? quoted EOF)
           (panic! "Encountered single quote at EOF"))
         (list 'quote quoted)))
      ((eof? c) EOF)
      (true
        (let ((list-head (cons c nil)))
          (read-symbol stream list-head)
          (symbol<-string (string<-char-list list-head)))))))

(defun skip-comment (stream)
  (let ((c (%read-char stream)))
    (if (not (eq? c 10)) ; '\n' 
      (skip-comment stream))))

(defun read-string (stream list-head)
  (let ((c (%read-char stream)))
    (cond
      ((eq? c 34) nil)
      (true
        (set-cdr list-head (cons c nil))
        (read-string stream (cdr list-head))))))

(defun read-fixnum (stream acc)
  (let ((c (%read-char stream)))
    (cond
      ((and (<= 48 c) (<= c 57))
       (read-fixnum stream (+ (* acc 10) (- c 48))))
      (true
        (back-up-char c)
        acc))))

(defun read-list (stream list-head)
  (let ((c (%read-char stream)))
    (cond
      ((whitespace? c) (read-list stream list-head))
      ((eq? c 41) nil)
      (true (back-up-char c)
            (let ((next (read-from-stream stream)))
              (set-cdr list-head (if (eq? next (symbol<-string "."))
                                   (read-from-stream stream)
                                   (cons next nil))))
            (read-list stream (cdr list-head))))))

(defun read-symbol (stream list-head)
  (let ((c (%read-char stream)))
    (cond
      ((symbol-char? c)
       (set-cdr list-head (cons c nil))
       (read-symbol stream (cdr list-head)))
      (true
        (back-up-char c)))))

;; TODO: this should check for the nucleus EOF, not the C one
(defun eof? (c)
  (< c 0))

(defun symbol-char? (c)
  (and (not (eof? c))
       (not (member? c (list 59 32 10 34 39 40 41)))))

(defun whitespace? (c)
  ;; ' ' or '\n' or '\t' 
  (or (eq? c 32) (eq? c 10) (eq? c 9)))

;;; TODO: Once we have varargs we should define this as a variable arity
;;; function rather than having a compiler builtin.
;;; TODO: Also once we have varargs we can easily create a wrapper that appends
;;; a newline rather than doing it by default
(defun %format (stream-spec format-str arg-list)
  (let ((stream (case stream-spec
                  (true $stdout)
                  (nil (make-string-stream ""))
                  (default stream-spec)))
        (len (length format-str)))
    (upto len
          (lambda (i)
            (let ((c (char-at format-str i)))
              (cond
                ((eq? c 126)
                 (assert! (not (nil? arg-list)) "Not enough args to format.")
                 (print-to-stream stream (car arg-list))
                 (set arg-list (cdr arg-list)))
                (true (write-char-to-stream stream c))))))
    ;; Hacky - we generally want to print a trailing newline when we're
    ;; printing debug stuff, but not for formatting strings and such. This is a
    ;; stopgap measure until we have a nice wrapper function that appends a
    ;; newline.
    (when (eq? stream-spec true)
      (write-char-to-stream stream 10))
    (if (eq? stream-spec nil)
      (string<-string-stream stream)
      nil)))
